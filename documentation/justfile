set dotenv-load := false
set positional-arguments

COLOR := "\\033[0;35m"
NO_COLOR := "\\033[0m"

# Show all available recipes
@_default:
    printf "\n{{ COLOR }}# Documentation (path: \`documentation/\`)\n"
    printf "========================================{{ NO_COLOR }}\n"
    just --list --unsorted


# Install dependencies
install:
    pipenv install --dev

##########
# Sphinx #
##########

# Create a new project with proposal and implementation plans
init-project name *ips:
    #!/usr/bin/env bash
    set -eo pipefail
    TIMESTAMP=$(date '+%Y%m%d')
    DATE=$(date '+%Y-%m-%d')
    PROJECTFS="{{ replace(lowercase(name), ' ', '_') }}"
    # Create project directory
    mkdir "projects/proposals/$PROJECTFS"
    # Create project directory index
    INDEXPATH="projects/proposals/$PROJECTFS/index.md"
    sed \
      -e '1,3d;$d' \
      -e 's/{Project title}/{{ name }}/g' \
      projects/templates/index.md > $INDEXPATH
    echo "Project index: $INDEXPATH"
    # Create project proposal
    PROPOSALPATH="projects/proposals/$PROJECTFS/$TIMESTAMP-project_proposal.md"
    sed \
      -e '1,3d;$d' \
      -e 's/{Project proposal title}/{{ name }}/g' \
      -e "s/{YYYY-MM-DD}/$DATE/g" \
      projects/templates/project_proposal.md > $PROPOSALPATH
    echo "Project proposal: $PROPOSALPATH"
    # Create implementation plans
    shift
    for IP in "$@"; do
        just create-ip "projects/proposals/$PROJECTFS" "$IP"
    done

# Create a new implementation plan in the given directory
create-ip directory name:
    #!/usr/bin/env bash
    set -eo pipefail
    TIMESTAMP=$(date '+%Y%m%d')
    DATE=$(date '+%Y-%m-%d')
    IPFS="{{ replace(lowercase(name), ' ', '_') }}"
    PROPOSALFS=$(find "{{ directory }}" -maxdepth 1 -name '*project_proposal.md')
    # Create implementation plan
    IPPATH="{{ directory }}/$TIMESTAMP-implementation_plan_$IPFS.md"
    sed \
      -e '1,3d;$d' \
      -e "s/{YYYY-MM-DD}/$DATE/g" \
      -e 's/{Implementation plan title}/{{ name }}/g' \
      -e "s|{Project proposal path}|/$PROPOSALFS|g" \
      projects/templates/implementation_plan.md > $IPPATH
    echo "Implementation plan: $IPPATH"

# Serve a live-reload documentation site
live port="50230": clean
    pipenv run sphinx-autobuild -b html . _serve/ --port {{ port }}

# Compile the documentation into a static site
build *args: clean
    pipenv run sphinx-build {{ args }} -b html . _build/

# Serve the compiled docs using a simple HTTP server
serve port="50231":
    pipenv run python -m http.server -d _build/ {{ port }}

# Delete the live server and build output directories
clean:
    rm -rf _serve/ _build
