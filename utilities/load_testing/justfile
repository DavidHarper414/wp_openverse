set dotenv-load := true

DOCKER_HOST := if os() == "macos" { "host.docker.internal" } else { "172.17.0.1" }

build:
    docker build . -t openverse-load_testing:latest

run script host: build
    docker run --rm -it -v {{ invocation_directory() }}:/app openverse-load-testing:latest ./{{ script }}.sh {{ host }}

api host=("http://" + DOCKER_HOST + ":8000"):
    @just run api {{ host }}

k6 script="main.js" +extra_args="": build
    docker run \
      --rm \
      ${ACCESS_TOKEN:+ --env ACCESS_TOKEN=$ACCESS_TOKEN} \
      ${API_URL:+ --env API_URL=$API_URL} \
      --volume {{ invocation_directory() }}/k6:/app \
      {{ extra_args }} \
      openverse-load_testing:latest \
      run {{ script }}
